(* printing '*' %\ensuremath{\times}% #&times;# *)
(* printing * %\ensuremath{\times}% #&times;# *)
(** printing '×' %\ensuremath{\times}% #&times;# *)
(** printing × %\ensuremath{\times}% #&times;# *)
(** printing ★ %\ensuremath{\star}% #&#9733;# *)
(** printing "★" %\ensuremath{\star}% #&#9733;# *)
(** printing '★' %\ensuremath{\star}% #&#9733;# *)
(** printing '⊔' %\ensuremath{\sqcup}% #&#x2294;# *)
(** printing ⊔ %\ensuremath{\sqcup}% #&#x2294;# *)
(** printing ∪ %\ensuremath{\cup}% #&cup;# *)
(** printing '∪' %\ensuremath{\cup}% #&cup;# *)
(** printing ∩ %\ensuremath{\cap}% #&cap;# *)
(** printing '∩' %\ensuremath{\cap}% #&cap;# *)
(** printing 'π' %\ensuremath{\pi}% #&pi;# *)
(** printing π %\ensuremath{\pi}% #&pi;# *)
(** printing 'Σ' %\ensuremath{\Sigma}% #&Sigma;# *)
(** printing Σ %\ensuremath{\Sigma}% #&Sigma;# *)
(** printing ⁰ %\ensuremath{^0}% #<sup>0</sup># *)
(** printing ¹ %\ensuremath{^1}% #<sup>1</sup># *)
(** printing ² %\ensuremath{^2}% #<sup>2</sup># *)
(** printing ³ %\ensuremath{^3}% #<sup>3</sup># *)
(** printing ⁴ %\ensuremath{^4}% #<sup>4</sup># *)
(** printing ⁵ %\ensuremath{^5}% #<sup>5</sup># *)
(** printing ⁶ %\ensuremath{^6}% #<sup>6</sup># *)
(** printing ⁷ %\ensuremath{^7}% #<sup>7</sup># *)
(** printing ⁸ %\ensuremath{^8}% #<sup>8</sup># *)
(** printing ⁹ %\ensuremath{^9}% #<sup>9</sup># *)
(** printing ⁱ %\ensuremath{^i}% #<sup>i</sup># *)
(** printing ⁺ %\ensuremath{^+}% #<sup>+</sup># *)
(** printing ⁻ %\ensuremath{^-}% #<sup>-</sup># *)
(** printing ⁼ %\ensuremath{^=}% #<sup>=</sup># *)
(** printing ⁽ %\ensuremath{^(}% #<sup>(</sup># *)
(** printing ⁾ %\ensuremath{^)}% #<sup>)</sup># *)
(** printing ⁿ %\ensuremath{^n}% #<sup>n</sup># *)
(** printing ₀ %\ensuremath{_0}% #<sub>0</sub># *)
(** printing ₁ %\ensuremath{_1}% #<sub>1</sub># *)
(** printing ₂ %\ensuremath{_2}% #<sub>2</sub># *)
(** printing ₃ %\ensuremath{_3}% #<sub>3</sub># *)
(** printing ₄ %\ensuremath{_4}% #<sub>4</sub># *)
(** printing ₅ %\ensuremath{_5}% #<sub>5</sub># *)
(** printing ₆ %\ensuremath{_6}% #<sub>6</sub># *)
(** printing ₇ %\ensuremath{_7}% #<sub>7</sub># *)
(** printing ₈ %\ensuremath{_8}% #<sub>8</sub># *)
(** printing ₉ %\ensuremath{_9}% #<sub>9</sub># *)
(** printing ₊ %\ensuremath{_+}% #<sub>+</sub># *)
(** printing ₋ %\ensuremath{_-}% #<sub>-</sub># *)
(** printing ₌ %\ensuremath{_=}% #<sub>=</sub># *)
(** printing ₍ %\ensuremath{_(}% #<sub>(</sub># *)
(** printing ₎ %\ensuremath{_)}% #<sub>)</sub># *)
(** printing ₐ %\ensuremath{_a}% #<sub>a</sub># *)
(** printing ₑ %\ensuremath{_e}% #<sub>e</sub># *)
(** printing ₒ %\ensuremath{_o}% #<sub>o</sub># *)
(** printing ₓ %\ensuremath{_x}% #<sub>x</sub># *)
(** printing ᵒᵖ %\ensuremath{^{\text{op}}}% #<sup>op</sup># *)
(** printing π₁ %\ensuremath{\pi_1}% #&pi;<sub>1</sub># *)
(** printing π₂ %\ensuremath{\pi_2}% #&pi;<sub>2</sub># *)
(** printing 'π₁' %\ensuremath{\pi_1}% #&pi;<sub>1</sub># *)
(** printing 'π₂' %\ensuremath{\pi_2}% #&pi;<sub>2</sub># *)
(** printing f₀ %\ensuremath{f_0}% #f<sub>0</sub># *)
(** printing f₀) %\ensuremath{f_0})% #f<sub>0</sub>)# *)
(** printing f₁ %\ensuremath{f_1}% #f<sub>1</sub># *)
(** printing f₁) %\ensuremath{f_1})% #f<sub>1</sub>)# *)
(** printing ≅ %\ensuremath{\cong}% #&cong;# *)
(** printing ≃ %\ensuremath{\simeq}% #&#x2243;# *)
(** printing ≄ %\ensuremath{\not\simeq}% #&#8772;# *)
(** printing λ %\ensuremath{\lambda}% #&lambda;# *)
(** printing 'o' %\ensuremath{\circ}% #&#x25cb;# *)
(** printing o %\ensuremath{\circ}% #&#x25cb;# *)
(** printing i ⁻¹ %\ensuremath{i^{-1}}% #i<sup>-1</sup># *)
(** printing 'i ⁻¹' %\ensuremath{i^{-1}}% #i<sup>-1</sup># *)
(** printing "i ⁻¹" %\ensuremath{i^{-1}}% #i<sup>-1</sup># *)
(** printing ⁻¹ %\ensuremath{^{-1}}% #<sup>-1</sup># *)
(** printing '⁻¹' %\ensuremath{^{-1}}% #<sup>-1</sup># *)
(** printing '⁻' %\ensuremath{^{-}}% #<sup>-</sup># *)
(** printing '¹' %\ensuremath{^{1}}% #<sup>1</sup># *)
(** printing ⁻ %\ensuremath{^{-}}% #<sup>-</sup># *)
(** printing ¹ %\ensuremath{^{1}}% #<sup>1</sup># *)
(** printing :> %:\ensuremath{>}% #:># *)
(** printing ':>' %:\ensuremath{>}% #:># *)
(** printing _1_ %\ensuremath{\text{\underline{1}}}% #<u>1</u># *)
(** printing '_1_' %\ensuremath{\text{\underline{1}}}% #<u>1</u># *)
(** printing _2_ %\ensuremath{\text{\underline{2}}}% #<u>2</u># *)
(** printing '_2_' %\ensuremath{\text{\underline{2}}}% #<u>2</u># *)
(** printing ℝ %\ensuremath{\mathbb{R}}% #&#x211d;# *)
(** printing ℝ³ %\ensuremath{\mathbb{R}^3}% #&#x211d;<sup>3</sup># *)
(** printing ℕ %\ensuremath{\mathbb{N}}% #&#x2115;# *)
(** printing ℤ %\ensuremath{\mathbb{Z}}% #&#x2124;# *)
(** printing ℙ %\ensuremath{\mathbb{P}}% #&#x2119;# *)
(** printing ← %\ensuremath{\leftarrow}% #&larr;# *)
(** printing ↑ %\ensuremath{\uparrow}% #&uarr;# *)
(** printing → %\ensuremath{\rightarrow}% #&rarr;# *)
(** printing ↓ %\ensuremath{\downarrow}% #&darr;# *)
(** printing ↔ %\ensuremath{\leftrightarrow}% #&harr;# *)
(** printing ↕ %\ensuremath{\updownarrow}% #&#x2195# *)
(** printing ↖ %\ensuremath{\nwarrow}% #&#x2196# *)
(** printing ↗ %\ensuremath{\nearrow}% #&#x2197# *)
(** printing ↘ %\ensuremath{\searrow}% #&#x2198# *)
(** printing ↙ %\ensuremath{\swarrow}% #&#x2199# *)
(** printing ⇉ %\ensuremath{\rightrightarrows} #&#x21c9# *)
(** printing ↦ %\ensuremath{\mapsto}% #&#21A6# *)
(** printing ↪ %\ensuremath{\hookrightarrow}% #&rarrhk;# *)
(** printing ⊆ %\ensuremath{\subseteq}% #&sube;# *)
(** printing ⊂ %\ensuremath{\subset}% #&sub;# *)
(** printing ∈ %\ensuremath{\in}% #&isin;# *)
(** printing ∅ %\ensuremath{\emptyset}% #&empty;# *)
(** printing ▻ %\ensuremath{\triangleright}% #&#x25BB;# *)
(** printing ◅ %\ensuremath{\triangleleft}% #&#x25C5;# *)
(** printing ≤ %\ensuremath{\le}% #&le;# *)
(** printing ≤) %\ensuremath{\le)}% #&le;)# *)
(** printing ≰ %\ensuremath{\not\le}% #&#x2270;# *)
(** printing ≱ %\ensuremath{\not\ge}% #&#x2271;# *)
(** printing ≼ %\ensuremath{\preceq}% #&#x227c;# *)
(** printing ≽ %\ensuremath{\succeq}% #&#x227d;# *)
(** printing ∧ %\ensuremath{\wedge}% #&and;# *)
(** printing ∨ %\ensuremath{\vee}% #&or;# *)
(** printing ¬ %\ensuremath{\neg}% #&not;# *)
(** printing ¬( %\ensuremath{\neg}(% #&not;(# *) (* ))) *)
(* must \usepackage{mathabx} in LaTeX *)
(** printing ↷ %\ensuremath{\lefttorightarrow}% #<div style="display:inline-block; transform:rotate(90deg);-o-transform:rotate(90deg);-mod-transform:rotate(90deg);-webkit-transform:rotate(90deg);">&#x21ba;</div># *)

Require Import Utf8.
Require Import JMeq ProofIrrelevance.
Require Import Category CommaCategory DiscreteCategory ComputableCategory CategoryIsomorphisms InitialTerminalCategory.
Require Import Common Notations FEqualDep.

Set Implicit Arguments.

Generalizable All Variables.

(** ------------------------------------------------------------------------ *)

(** * Exercise 4.6.4.3 *)
Section Exercise_4_6_4_3.
  (** ** Problem *)
  (** Let [C] be a category and let [c c' : Ob C] be objects. Consider
      them as functors [c c' : 1 -> C], and consider the setup [c : 1
      -> C <- 1 : c']. What is the comma category [c ↓ c']? *)
  (** ** Solution *)
  (** We have [c ↓ c' ≅ Disc(c -> c')], the discrete category on the
      morphisms between [c] and [c']. *)

  Context `(C : @Category objC).
  Variables c c' : C.

  (** Define how the functor [c ↓ c' -> Disc(c -> c')] acts on morphisms, by unfolding
      definitions, rewriting with the definition of a category, and
      using assumptions. *)

  Definition Exercise_4_6_4_3_F_MorphismOf s d (m : Morphism (c ↓ c') s d)
  : Morphism (DiscreteCategory (Morphism C c c')) (projT2 s) (projT2 d).
    destruct s, d, m; simpl in *;
    destruct_sig;
    autorewrite with morphism in *;
    assumption.
  Qed.

  (** Prove that the action on morphisms extends to a functor [c ↓ c'
  -> Disc(c -> c')] by applying proof-irrelevance. *)

  Definition Exercise_4_6_4_3_F : Functor (c ↓ c') (DiscreteCategory (Morphism C c c')).
    refine {| MorphismOf := Exercise_4_6_4_3_F_MorphismOf |};
    abstract (simpl; intros; apply ProofIrrelevance.proof_irrelevance).
  Defined.

  (** Define the action of the functor [Disc(c -> c') -> c ↓ c'] on objects. *)

  Let G_ObjectOf (x : DiscreteCategory (Morphism C c c')) : (c ↓ c')%category
    := existT _ (tt, tt) x
       : CommaCategory_ObjectT (FunctorFromTerminal _ c) (FunctorFromTerminal _ c').

  (** Define the action of the functor [Disc(c -> c') -> c ↓ c'] on
      morphisms by unfolding definitions, using reflexivity, and
      rewriting with the definition of a category. *)

  Definition Exercise_4_6_4_3_G_MorphismOf
             s d (m : Morphism (DiscreteCategory (Morphism C c c')) s d)
  : Morphism (c ↓ c') (G_ObjectOf s) (G_ObjectOf d).
    subst_body.
    revert s d m.
    repeat (intros [] || intro).
    simpl in *.
    constructor; simpl.
    exists (eq_refl, eq_refl).
    abstract (
        autorewrite with morphism;
        reflexivity
      ).
  Defined.

  (** Now we show that the action on morphisms extends to a functor
      [Disc(c -> c') -> c ↓ c'] by unfolding definitions, using
      reflexivity, using the definition of equality of dependent pairs
      (and using proof-irrelevance (via [simpl_eq]) when doing this),
      using the fact that [x = y -> f x = f y], and using the
      substitution property of equality. *)

  Definition Exercise_4_6_4_3_G : Functor (DiscreteCategory (Morphism C c c')) (c ↓ c').
    refine {| MorphismOf := Exercise_4_6_4_3_G_MorphismOf |};
    subst_body;
    abstract (
        repeat match goal with
                 | _ => reflexivity
                 | _ => intro
                 | _ => progress simpl in *
                 | _ => progress simpl_eq
                 | _ => progress subst
                 | _ => progress expand
                 | _ => apply f_equal
               end
      ).
  Defined.

  (** Now we prove the isomorphism in [Cat].  We declare the existance
      of the inverse functors, and then prove that they are inverses
      by applying the definition of equality of functors, applying
      proof-irrelevance, using reflexivity, unfolding definitions,
      using the definition of equality of dependent pairs (along with
      proof-irrelevance), using the fact that [x = y -> f x = f y],
      and generalizing some variables being matched on. *)

  Lemma Exercise_4_6_4_3
  : ((DiscreteCategory (Morphism C c c') : CategoryOfCategories)
       ≅ (c ↓ c' : CategoryOfCategories))%category.
    exists Exercise_4_6_4_3_G.
    exists Exercise_4_6_4_3_F;
      abstract (
          simpl;
          apply Functor_Eq;
          repeat match goal with
                   | _ => reflexivity
                   | _ => progress simpl in *
                   | _ => intro
                   | _ => apply ProofIrrelevance.proof_irrelevance
                   | _ => progress simpl_eq
                   | _ => progress destruct_head_hnf @CommaCategory_Object
                   | _ => progress destruct_sig
                   | _ => progress destruct_head_hnf @prod
                   | _ => progress destruct_head_hnf unit
                   | _ => progress expand
                   | _ => apply f_equal
                   | _ => apply CommaCategory_Morphism_Eq
                   | [ |- context[match ?f ?x with _ => _ end] ] => generalize (f x); intro
                 end
        ).
  Defined.
End Exercise_4_6_4_3.
(** ------------------------------------------------------------------------ *)
